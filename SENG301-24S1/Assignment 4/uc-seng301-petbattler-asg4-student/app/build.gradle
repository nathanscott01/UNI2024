import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // This dependency is used by the application.
    implementation libs.guava

    // use hibernate to persist (java) domain entities for us, aka JPA implementation
    implementation 'org.hibernate:hibernate-core:6.4.3.Final'

    // use a in-memory database to store entities (can be substituted with any database)
    implementation 'com.h2database:h2:2.2.224'

    // Sqlite as persistent DB
    implementation 'org.xerial:sqlite-jdbc:3.45.1.0'
    implementation 'org.hibernate.orm:hibernate-community-dialects:6.4.3.Final'

    //Cucumber dependencies
    testImplementation 'io.cucumber:cucumber-java:7.15.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.15.0'
    testImplementation 'org.junit.platform:junit-platform-suite:1.10.2'

    // Logging facility (SENG301 lecture 9 and lab 6)
    implementation 'org.apache.logging.log4j:log4j-core:2.23.0'

    // JSON deserialization (for external REST API, lab 6)
    implementation 'com.fasterxml.jackson.core:jackson-core:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'

    // Mocking (lab 6)
    implementation 'org.mockito:mockito-core:5.11.0'
}

run {
    // this is needed if you want to start your main class with gradle run
    // you may pass the --console=PLAIN option to disable the green progress bar
    // e.g., gradlew run --console=PLAIN
    standardInput = System.in
}

application {
    // Define the main class for the application.
    mainClass = 'uc.seng301.petbattler.asg4.App'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
    filter {
        includeTestsMatching "uc.seng301.petbattler.asg4.unittests.*"
    }
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

tasks.register("cucumber", Test) {
    testLogging.showStandardStreams = true
    useJUnitPlatform()
    filter {
        includeTestsMatching "uc.seng301.petbattler.asg4.cucumber.*"
    }
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat TestExceptionFormat.FULL
    }
}